plugins {
    id 'com.github.johnrengelman.shadow' version "8.1.1"
    id 'java'
}

group 'me.yamakaja.commanditems'
version '2.6.4-pre'

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

processResources {
    filesMatching("**/plugin.yml") {
        filter {
            it.replace('${version}', project.getVersion())
        }
    }
}

repositories {
    mavenCentral()
    mavenLocal()

    maven { url "https://hub.spigotmc.org/nexus/content/groups/public/" }
    maven { url "https://repo.codemc.org/repository/maven-public/" }
    maven { url "https://repo.aikar.co/content/groups/aikar/" }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.15.2-R0.1-SNAPSHOT"

    implementation "org.bstats:bstats-bukkit:3.0.0"

    implementation "co.aikar:acf-paper:0.5.1-SNAPSHOT"

    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.14.1"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.14.1"

    implementation "de.tr7zw:item-nbt-api:2.11.3"
}

shadowJar {
    archiveBaseName = 'CommandItems'

    relocate 'org.bstats', 'me.yamakaja.commanditems.lib.bstats'

    relocate 'co.aikar.commands', 'me.yamakaja.commanditems.lib.acf'
    relocate 'org.intellij', 'me.yamakaja.commanditems.lib.intellij'
    relocate 'org.jetbrains', 'me.yamakaja.commanditems.lib.jetbrains'
    relocate 'net.jodah', 'me.yamakaja.commanditems.lib.jodah'

    relocate 'org.yaml.snakeyaml', 'me.yamakaja.commanditems.lib.snakeyaml'
    relocate 'com.fasterxml', 'me.yamakaja.commanditems.lib.fasterxml'

    relocate 'de.tr7zw.changeme.nbtapi', 'me.yamakaja.commanditems.lib.nbtapi'
}

task install(type: Copy) {
    from("$rootDir/build/libs/") {
        include "**/CommandItems-*.jar"
    }
    rename("([^-]*).*\\.jar", '$1.jar')
    into "$rootDir/testenv/plugins/"
}
