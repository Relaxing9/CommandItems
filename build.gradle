plugins {
    id 'com.github.johnrengelman.shadow' version "8.1.1"
    id 'java'
}

group 'me.yamakaja.commanditems'
version '2.6.4'

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

processResources {
    filesMatching("**/plugin.yml") {
        filter {
            it.replace("${version}", project.getVersion())
        }
    }
}

repositories {
    mavenCentral()
    mavenLocal()

    maven { url "https://hub.spigotmc.org/nexus/content/groups/public/" }
    maven { url "https://repo.codemc.org/repository/maven-public/" }
    maven { url "https://repo.aikar.co/content/groups/aikar/" }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.15.2-R0.1-SNAPSHOT"

    implementation "org.bstats:bstats-bukkit:3.0.2"

    implementation "co.aikar:acf-paper:0.5.1-SNAPSHOT"

    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.17.0"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.16.2"

    implementation "de.tr7zw:item-nbt-api:2.12.1"
}

shadowJar {
    archiveBaseName = 'CommandItems'

    relocate 'org.bstats', "${group}.lib.bstats"

    relocate 'co.aikar.commands', "${group}.lib.acf"
    relocate 'org.intellij', "${group}.lib.intellij"
    relocate 'org.jetbrains', "${group}.lib.jetbrains"
    relocate 'net.jodah', "${group}.lib.jodah"

    relocate 'org.yaml.snakeyaml', "${group}.lib.snakeyaml"
    relocate 'com.fasterxml', "${group}.lib.fasterxml"

    relocate 'de.tr7zw.changeme.nbtapi', "${group}.lib.nbtapi"
}

task install(type: Copy) {
    from("$rootDir/build/libs/") {
        include "**/CommandItems-*.jar"
    }
    rename("([^-]*).*\\.jar", '$1.jar')
    into "$rootDir/testenv/plugins/"
}
